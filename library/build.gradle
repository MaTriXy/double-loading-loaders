apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'


def groupMode = 'il.co.galex'
def versioning = '1.1.0'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName versioning
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishNonDefault true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.1'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

publishing {
    publications {
        dll(MavenPublication) {
            groupId groupMode
            artifactId 'double-loading-loaders'
            version versioning
            artifact(sourceJar)
            artifact(androidJavadocsJar)
            artifact("$buildDir/outputs/aar/library-release.aar")
        }
    }
}

bintray {
    user bintrayUser
    key bintrayPass
    pkg {
        repo = 'maven'
        name = 'double-loading-loaders'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/galex/double-loading-loaders.git'

        version {
            name = versioning
            desc = 'Load cache and network data in a beautiful way'
            released = new Date()
            vcsTag = versioning
            publications = ['dll']
        }
    }
}
